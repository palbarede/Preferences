# piger's prompt
# Daniel Kertesz <daniel@spatof.org>

prompt_piger_help() {
    cat <<EOH
This prompt takes advantage of 'vcs_info' to display useful VCS informations in the prompt.
You can chose between the short and the extended format, where the latter will also display
the typical "username@hostname" part of the prompt.
EOH
}

# hash the system's hostname letters to generate a color
prompt_piger_hostname_color() {
    local chash=0
    foreach letter (${(ws::)HOST[(ws:.:)1]}) (( chash+=#letter )) end
    printf "%03d" $(( $chash % 255 ))
}

prompt_piger_setup() {
    local extended=$1

    # this is usually defined by 'promptinit'
    [[ -z $prompt_newline ]] && prompt_newline=$'\n%{\r%}'

    autoload -Uz add-zsh-hook
    autoload -Uz vcs_info

    zstyle ':vcs_info:*' actionformats \
           '%F{64}[%F{70}%b%F{3}|%F{1}%a%F{64}]%f %m%u%c'
    zstyle ':vcs_info:*' formats \
           '%F{64}[%F{70}%b%F{64}]%f %m%u%c'
    zstyle ':vcs_info:(sv[nk]|bzr):*' branchformat '%b%F{1}:%F{3}%r'
    zstyle ':vcs_info:*' enable git svn
    zstyle ':vcs_info:*' check-for-changes true
    zstyle ':vcs_info:*' unstagedstr ' %F{3}★%f'
    zstyle ':vcs_info:*' stagedstr ' %F{2}●%f'
    zstyle ':vcs_info:*' use-quilt true

    add-zsh-hook precmd vcs_info

    PROMPT='%F{027}[%T] %F{208}%3~ ${vcs_info_msg_0_}${prompt_newline}'
    if [[ $SSH_CONNECTION != "" || $UID -eq 0 || $extended != "" ]]; then
        PROMPT+='%F{$(prompt_piger_hostname_color)}%n%f@%F{$(prompt_piger_hostname_color)}%m%f%(1j.|%j.) '
    else
        PROMPT+='%(1j.|%j.)'
    fi
    PROMPT+='%F{$(prompt_piger_hostname_color)}%(!.#.${PIGER_PROMPT_SYMBOL:-❯})%f '

    # Use RPROMPT to display the exit status of the last executed command when non-zero.
    RPROMPT=$'%(?..%B%F{124}✘%f %?%b) ${rbenv_prompt}'

    # Spell checker prompt.
    # default: correct 'mano' to 'nano' [nyae]?
    SPROMPT="٩(͡๏̯͡๏)۶ correct '%R' to '%r' ? ([Y]es/[N]o/[E]dit/[A]bort) "

    # set prompt options through promptinit
    prompt_opts=(cr subst percent)
}

prompt_piger_preview() {
    prompt_preview_theme piger "$@"
}

prompt_piger_setup "$@"
